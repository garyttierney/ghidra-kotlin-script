plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
}

group 'com.github.garyttierney'
version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"}
}

def ghidraInstallDir

if (System.env.GHIDRA_INSTALL_DIR) {
    ghidraInstallDir = System.env.GHIDRA_INSTALL_DIR
} else if (project.hasProperty("GHIDRA_INSTALL_DIR")) {
    ghidraInstallDir = project.getProperty("GHIDRA_INSTALL_DIR")
}

if (ghidraInstallDir) {
    apply from: new File(ghidraInstallDir).getCanonicalPath() + "/support/buildExtension.gradle"
} else {
    throw new GradleException("GHIDRA_INSTALL_DIR is not defined!")
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.7.0"
    implementation "org.jetbrains.kotlinx:ki-shell:0.5.1"

    implementation 'com.github.JetBrains:jediterm:master-SNAPSHOT'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    runtimeOnly fileTree(dir: ghidraInstallDir + '/Ghidra/patch', include: "**/*.jar")
    runtimeOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Configurations', include: "**/*.jar")
    runtimeOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Features', include: "**/*.jar")
    runtimeOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Framework', include: "**/*.jar")
    runtimeOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Processors', include: "**/*.jar")
    runtimeOnly fileTree(dir: ghidraInstallDir + '/Ghidra/Debug', include: "**/*.jar")
}

test {
    useJUnitPlatform()
}

task cleanDependencyJars(type: Delete) {
    delete fileTree("lib").matching {
        include "**/*.jar"
    }
}

tasks.buildExtension.dependsOn(copyDependencies)
tasks.clean.dependsOn(cleanDependencyJars)